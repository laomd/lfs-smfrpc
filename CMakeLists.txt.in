cmake_minimum_required(VERSION 3.7.0)
project(build-deps NONE)
cmake_host_system_information(
  RESULT build_concurrency_factor
  QUERY NUMBER_OF_LOGICAL_CORES)
set(CMAKE_EXPORT_NO_PACKAGE_REGISTRY ON  CACHE "" INTERNAL FORCE)
set(CMAKE_FIND_PACKAGE_NO_SYSTEM_PACKAGE_REGISTRY ON  CACHE "" INTERNAL FORCE)
set(CMAKE_FIND_PACKAGE_NO_PACKAGE_REGISTRY ON  CACHE "" INTERNAL FORCE)
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(autotools_flags
  CFLAGS=-I@DEPS_INSTALL_DIR@/include
  CXXFLAGS=-I@DEPS_INSTALL_DIR@/include
  LDFLAGS=-L@DEPS_INSTALL_DIR@/lib)
set(PKG_CONFIG_PATH PKG_CONFIG_PATH=@DEPS_INSTALL_DIR@/lib/pkgconfig)
set(make_command make -j ${build_concurrency_factor})
set(info_dir --infodir=@DEPS_INSTALL_DIR@/share/info)

include(ExternalProject)
ExternalProject_Add(boost
  # The 1.67.0 release has a bug in Boost Lockfree around a missing header.
  URL https://dl.bintray.com/boostorg/release/1.64.0/source/boost_1_64_0.tar.gz
  URL_MD5 319c6ffbbeccc366f14bb68767a6db79
  INSTALL_DIR    @DEPS_INSTALL_DIR@
  PATCH_COMMAND
    ./bootstrap.sh
    --prefix=@DEPS_INSTALL_DIR@
    --with-libraries=atomic,chrono,date_time,filesystem,program_options,system,test,thread
  CONFIGURE_COMMAND ""
  BUILD_COMMAND ""
  INSTALL_COMMAND
    ${CMAKE_COMMAND} -E chdir <SOURCE_DIR>
    ./b2
    -j ${build_concurrency_factor}
    --layout=system
    --build-dir=<BINARY_DIR>
    install
    variant=release
    cflags=-fPIC
    cxxflags=-fPIC
    link=static
    threading=multi
    hardcode-dll-paths=true
    dll-path=@DEPS_INSTALL_DIR@/lib)

ExternalProject_Add(fmt
  URL https://github.com/fmtlib/fmt/archive/9e554999ce02cf86fcdfe74fe740c4fe3f5a56d5.tar.gz
  URL_MD5 e2869a5032a6cc35b4d14008b6125ed9
  INSTALL_DIR    @DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=@CMAKE_BUILD_TYPE@
    -DBUILD_SHARED_LIBS=@BUILD_SHARED_LIBS@
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DCMAKE_PREFIX_PATH=<INSTALL_DIR>
    -DFMT_INSTALL=ON
    -DFMT_DOC=OFF
    -DFMT_TEST=OFF)

ExternalProject_Add(zlib
  URL https://zlib.net/zlib-1.2.11.tar.gz
  URL_MD5 1c9f62f0778697a09d36121ead88e08e
  INSTALL_DIR @DEPS_INSTALL_DIR@
  CONFIGURE_COMMAND
    <SOURCE_DIR>/configure
    --prefix=<INSTALL_DIR>
  BUILD_COMMAND ${make_command}
  INSTALL_COMMAND ${make_command} install)


ExternalProject_Add(protobuf
  DEPENDS zlib
  URL https://github.com/protocolbuffers/protobuf/archive/v3.11.0.tar.gz
  URL_MD5 99ab003ca0e98c9dc40edbd60dd43633
  INSTALL_DIR   @DEPS_INSTALL_DIR@
  SOURCE_SUBDIR cmake
  CMAKE_ARGS
    -Dprotobuf_BUILD_TESTS=OFF
    -DCMAKE_BUILD_TYPE=@CMAKE_BUILD_TYPE@
    -DBUILD_SHARED_LIBS=@BUILD_SHARED_LIBS@
    -DCMAKE_PREFIX_PATH=<INSTALL_DIR>
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>)

ExternalProject_Add(c-ares
  URL https://github.com/c-ares/c-ares/archive/cares-1_14_0.tar.gz
  URL_MD5 79dcc4f2adaff431920755d9cafc9933
  INSTALL_DIR @DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCARES_VERSION="1.14.0"
    -DCMAKE_BUILD_TYPE=@CMAKE_BUILD_TYPE@
    -DBUILD_SHARED_LIBS=@BUILD_SHARED_LIBS@
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DCMAKE_PREFIX_PATH=<INSTALL_DIR>
    -DCARES_INSTALL=ON)

ExternalProject_Add(yaml-cpp
  URL https://github.com/jbeder/yaml-cpp/archive/yaml-cpp-0.6.0.tar.gz
  URL_MD5 8adc0ae6c2698a61ab086606cc7cf562
  INSTALL_DIR    @DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=@CMAKE_BUILD_TYPE@
    -DBUILD_SHARED_LIBS=@BUILD_SHARED_LIBS@
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DCMAKE_PREFIX_PATH=<INSTALL_DIR>
    -DYAML_CPP_BUILD_TOOLS=OFF
    -DYAML_CPP_BUILD_TESTS=OFF
  DEPENDS boost)

ExternalProject_Add(gmp
  URL https://gmplib.org/download/gmp/gmp-6.1.2.tar.bz2
  URL_MD5 8ddbb26dc3bd4e2302984debba1406a5
  INSTALL_DIR @DEPS_INSTALL_DIR@
  CONFIGURE_COMMAND
    <SOURCE_DIR>/configure
    --prefix=<INSTALL_DIR>
    --srcdir=<SOURCE_DIR>
    ${info_dir}/gmp
  BUILD_COMMAND ${make_command}
  INSTALL_COMMAND ${make_command} install)

ExternalProject_Add(nettle
  URL https://ftp.gnu.org/gnu/nettle/nettle-3.4.tar.gz
  URL_MD5 dc0f13028264992f58e67b4e8915f53d
  INSTALL_DIR @DEPS_INSTALL_DIR@
  CONFIGURE_COMMAND
    <SOURCE_DIR>/configure
    --prefix=<INSTALL_DIR>
    --srcdir=<SOURCE_DIR>
    --libdir=<INSTALL_DIR>/lib
    ${info_dir}/nettle
    ${autotools_flags}
  BUILD_COMMAND ${make_command}
  INSTALL_COMMAND ${make_command} install
  DEPENDS gmp)

ExternalProject_Add(gnutls
  URL https://www.gnupg.org/ftp/gcrypt/gnutls/v3.5/gnutls-3.5.18.tar.xz
  URL_MD5 c2d93d305ecbc55939bc2a8ed4a76a3d
  INSTALL_DIR @DEPS_INSTALL_DIR@
  CONFIGURE_COMMAND
   ${CMAKE_COMMAND} -E env ${PKG_CONFIG_PATH}
    <SOURCE_DIR>/configure
    --prefix=<INSTALL_DIR>
    --srcdir=<SOURCE_DIR>
    --with-included-unistring
    --with-included-libtasn1
    --without-idn
    --without-p11-kit
    # https://lists.gnupg.org/pipermail/gnutls-help/2016-February/004085.html
    --disable-non-suiteb-curves
    --disable-doc
    ${autotools_flags}
  BUILD_COMMAND ${make_command}
  INSTALL_COMMAND ${make_command} install
  DEPENDS gmp nettle)

ExternalProject_Add(lz4
  URL https://github.com/lz4/lz4/archive/v1.8.3.tar.gz
  URL_MD5 d5ce78f7b1b76002bbfffa6f78a5fc4e
  INSTALL_DIR   @DEPS_INSTALL_DIR@
  SOURCE_SUBDIR contrib/cmake_unofficial
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=@CMAKE_BUILD_TYPE@
    -DBUILD_SHARED_LIBS=@BUILD_SHARED_LIBS@
    -DCMAKE_PREFIX_PATH=<INSTALL_DIR>
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>)

ExternalProject_Add(numactl
  URL https://github.com/numactl/numactl/releases/download/v2.0.12/numactl-2.0.12.tar.gz
  URL_MD5 2ba9777d78bfd7d408a387e53bc33ebc
  INSTALL_DIR @DEPS_INSTALL_DIR@
  CONFIGURE_COMMAND
    <SOURCE_DIR>/configure
    --prefix=<INSTALL_DIR>
    --srcdir=<SOURCE_DIR>
  BUILD_COMMAND ${make_command}
  INSTALL_COMMAND ${make_command} install)

ExternalProject_Add(libpciaccess
  URL https://www.x.org/releases/individual/lib/libpciaccess-0.13.4.tar.gz
  URL_MD5 cc1fad87da60682af1d5fa43a5da45a4
  INSTALL_DIR @DEPS_INSTALL_DIR@
  CONFIGURE_COMMAND
    <SOURCE_DIR>/configure
    --prefix=<INSTALL_DIR>
    --srcdir=<SOURCE_DIR>
  BUILD_COMMAND ${make_command}
  INSTALL_COMMAND ${make_command} install)

ExternalProject_Add(hwloc
  URL https://download.open-mpi.org/release/hwloc/v1.11/hwloc-1.11.5.tar.gz
  URL_MD5 8f5fe6a9be2eb478409ad5e640b2d3ba
  INSTALL_DIR @DEPS_INSTALL_DIR@
  CONFIGURE_COMMAND
    <SOURCE_DIR>/configure
    --prefix=<INSTALL_DIR>
    --srcdir=<SOURCE_DIR>
  BUILD_COMMAND ${make_command}
  INSTALL_COMMAND ${make_command} install
  DEPENDS numactl libpciaccess)

ExternalProject_Add(xz
  URL https://github.com/xz-mirror/xz/releases/download/v5.2.2/xz-5.2.2.tar.gz
  URL_MD5 7cf6a8544a7dae8e8106fdf7addfa28c
  INSTALL_DIR @DEPS_INSTALL_DIR@
  CONFIGURE_COMMAND
    COMMAND
      <SOURCE_DIR>/configure
      --prefix=<INSTALL_DIR>
  BUILD_COMMAND ${make_command}
  INSTALL_COMMAND ${make_command} install)

ExternalProject_Add(xml2
  URL https://github.com/GNOME/libxml2/archive/f8a8c1f59db355b46962577e7b74f1a1e8149dc6.tar.gz
  URL_MD5 8717ac0b233af3ed1e47236afa6e7946
  INSTALL_DIR @DEPS_INSTALL_DIR@
  CONFIGURE_COMMAND
    COMMAND
      ${CMAKE_COMMAND} -E chdir <SOURCE_DIR>
      ${CMAKE_COMMAND} -E env NOCONFIGURE=true ./autogen.sh
    COMMAND
      <SOURCE_DIR>/configure
      --without-python
      --prefix=<INSTALL_DIR>
  BUILD_COMMAND ${make_command}
  INSTALL_COMMAND ${make_command} install
  DEPENDS xz zlib)

ExternalProject_Add(cryptopp
  URL https://github.com/weidai11/cryptopp/archive/CRYPTOPP_5_6_5.tar.gz
  URL_MD5 88224d9c0322f63aa1fb5b8ae78170f0
  INSTALL_DIR @DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=@CMAKE_BUILD_TYPE@
    -DBUILD_SHARED_LIBS=@BUILD_SHARED_LIBS@
    -DCMAKE_PREFIX_PATH=<INSTALL_DIR>
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DBUILD_TESTING=OFF)

ExternalProject_Add(lksctp-tools
  # Note: 1.0.18 has a bug w/ the build/configuration system that does not
  # produce sctp.h.in -> sctp.h and instead install sctp.h.in
  URL https://github.com/sctp/lksctp-tools/archive/lksctp-tools-1.0.17.tar.gz
  URL_MD5 910a4f1d4024d71149b91f50f97eae23
  INSTALL_DIR @DEPS_INSTALL_DIR@
  PATCH_COMMAND ./bootstrap
  CONFIGURE_COMMAND
    <SOURCE_DIR>/configure
    --prefix=<INSTALL_DIR>
    --srcdir=<SOURCE_DIR>
  BUILD_COMMAND ${make_command}
  INSTALL_COMMAND ${make_command} install)

ExternalProject_Add(ragel
  URL http://www.colm.net/files/ragel/ragel-6.10.tar.gz
  INSTALL_DIR    @DEPS_INSTALL_DIR@
  CONFIGURE_COMMAND
    <SOURCE_DIR>/configure
    --prefix=<INSTALL_DIR>
    --srcdir=<SOURCE_DIR>
    BUILD_COMMAND ${make_command}
    INSTALL_COMMAND ${make_command} install)

ExternalProject_Add(seastar
  URL https://github.com/vectorizedio/seastar/archive/master.tar.gz
  INSTALL_DIR    @DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=@CMAKE_BUILD_TYPE@
    -DBUILD_SHARED_LIBS=@BUILD_SHARED_LIBS@
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DCMAKE_PREFIX_PATH=<INSTALL_DIR>
    -DSeastar_INSTALL=ON
    -DSeastar_DPDK=OFF
    -DSeastar_APPS=OFF
    -DSeastar_DEMOS=OFF
    -DSeastar_DOCS=OFF
    -DSeastar_TESTING=OFF
    -DSeastar_CXX_FLAGS=-Wno-error
    -DSeastar_LD_FLAGS=-pthread
    -DSeastar_STD_OPTIONAL_VARIANT_STRINGVIEW=ON
    -DSeastar_CXX_DIALECT=c++17
    -DSeastar_UNUSED_RESULT_ERROR=ON
  DEPENDS
    fmt
    boost
    protobuf
    yaml-cpp
    c-ares
    gnutls
    lz4
    hwloc
    numactl
    xml2
    xz
    cryptopp
    lksctp-tools
    zlib
    ragel)
